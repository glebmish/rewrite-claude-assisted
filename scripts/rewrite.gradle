/**
 * OpenRewrite Init Script
 *
 * This init script applies OpenRewrite to a project for automated code analysis
 * without requiring permanent modifications to the project's build files.
 *
 * Usage:
 *   ./gradlew --init-script rewrite.gradle rewriteDryRun -DrecipeName=com.yourorg.YourRecipe
 *
 * The recipe name must be provided via the recipeName system property.
 * The recipe itself should be defined in a rewrite.yml file in the repository root.
 */

// Centralized version management
def rewriteAllVersion = '1.23.0'
def rewriteDockerVersion = '2.14.0'
def rewriteGithubActionsVersion = '3.14.0'
def rewriteJavaDependenciesVersion = '1.45.0'
def rewriteMigrateJava = '3.21.2'
def rewriteTestingFrameworksVersion = '3.21.3'

initscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath("org.openrewrite.rewrite:org.openrewrite.rewrite.gradle.plugin:7.3.0")
    }
}

rootProject {
    def recipeName = System.getProperty('recipeName')
    if (!recipeName) {
        throw new GradleException("""
            |Recipe name must be provided via system property.
            |Usage: ./gradlew --init-script rewrite.gradle rewriteDryRun -DrecipeName=com.yourorg.YourRecipe
            |
            |Make sure you have a rewrite.yml file in the repository root that defines this recipe.
        """.stripMargin())
    }

    plugins.apply(org.openrewrite.gradle.RewritePlugin)

    dependencies {
        rewrite("org.openrewrite.recipe:rewrite-all:${rewriteAllVersion}")
        rewrite("org.openrewrite.recipe:rewrite-docker:${rewriteDockerVersion}")
        rewrite("org.openrewrite.recipe:rewrite-github-actions:${rewriteGithubActionsVersion}")
        rewrite("org.openrewrite.recipe:rewrite-java-dependencies:${rewriteJavaDependenciesVersion}")
        rewrite("org.openrewrite.recipe:rewrite-migrate-java:${rewriteMigrateJava}")
        rewrite("org.openrewrite.recipe:rewrite-testing-frameworks:${rewriteTestingFrameworksVersion}")
    }

    rewrite {
        activeRecipe(recipeName)
        setExportDatatables(true)
        exclusion(
          "**/gradlew",
          "**/gradlew.bat",
          "**/gradle-wrapper.jar"
        )
    }
}
